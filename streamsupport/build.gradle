buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'ru.vyarus.animalsniffer'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

apply plugin: 'me.tatarka.retrolambda'

retrolambda {
    jvmArgs '-noverify'
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
    //defaultMethods true
    //incremental false
}

sourceSets {
  main {
      java {
          srcDir '../src/main'
//          exclude 'java/java/**'
//          exclude '../src/main/java/java/**'
          exclude '../src/main/java/build/**'
      }
  }
  test {
      java {
          srcDir '../src/tests'
          exclude '../src/main/java/build/**'
     }
  }
}

repositories {
    jcenter()
}

dependencies {
    signature 'org.codehaus.mojo.signature:java16:1.1@signature'
    testCompile 'junit:junit:3.8.2'
    testCompile 'org.testng:testng:6.8.17'
    testCompile project(':streamsupport-atomic')
    testCompile project(':streamsupport-cfuture')
    testCompile project(':streamsupport-flow')
    testCompile project(':streamsupport-literal')
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "passed", "skipped", "failed", "standardOut", "standardError"
    showStandardStreams = true
  }
}

apply from: 'javadoc.gradle'

tasks.withType(JavaCompile) {
    //options.encoding = 'UTF-8'
    options.encoding = 'Cp1252'
}

apply plugin: 'jacoco'

jacocoTestReport {
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories =  files(sourceSets.main.output)
    executionData = files(jacocoTestReport.executionData)
    reports {
        xml.enabled = true // coveralls needed
        html.enabled = true
    }
}

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    //sourceDirs = sourceSets.main.allSource.srcDirs.flatten()
    //sourceDirs = sourceSets.main.allSource.srcDirs
    //sourceDirs = files(sourceSets.main.allSource.srcDirs)
    //sourceDirs = fileTree(sourceSets.main.allSource.srcDirs)
    sourceDirs = files(sourceSets.main.allSource.srcDirs).files.absolutePath
    jacocoReportPath "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
}

tasks.jacocoTestReport {
    dependsOn 'test'
}

tasks.coveralls {
    dependsOn 'jacocoTestReport'
}

animalsnifferMain {
    exclude('sun.misc.Unsafe')
}

animalsniffer {
    ignoreFailures = true
    annotation = 'build.IgnoreJava8API'
}
